define constant, begin
	-90, slottop
	-35, slotcenter
	-1050, slotbottom
	6, spinrate
end

plotscript, slotmachine, begin
# variables
	variable (i, sli, check)
	
# init
	suspendcontrol
	screenfadeout()
	sli := loadSliceCollection(1)
	slice to back (sli)
	for (i,0,2) do (
		sli := firstchild (slicechild (firstchild (lookupslice (sli:slot)), i))
		set slice y (sli, random (1,17)*-60 + slotcenter)
		set slice extra(sli, 0, 1)
		state:slotspin := slots:spin
	)
	$3=""
	screenfadein()
	suspendcontrol
	
	state:control := state:slots
# main
	while (state:slotspin == slots:spin) do (
		specwait
	)
	suspendcontrol
	specwait (20)
	slotwincheck
	specwait (20)
	if (string length(3) == 0) then (specwait(90))
# finish
	screenfadeout()
	freeSlice (lookup slice(sli:slot))
	show no value
	screenfadein()
	if (string length(3) == 0) then (write(9)) else (write(10),write(8))

end

#script to spin slots
script, spin, begin
	variable (i, sli, check)
	for (i, 0, 2) do (
		state:slotspin := slots:null
		sli := firstchild (slicechild (firstchild (lookupslice (sli:slot)), i))
		if (get slice extra (sli, 0)==1) then (
			set slice y (sli, slicey(sli)+spinrate)
			if (slice y(sli)>slottop) then (set slice y (sli, slotbottom))
			state:slotspin := slots:spin
		)
	)
	sli := firstChild(lookupSlice(sli:slot))
	
	if (game:tick,mod,8==0) then (
		if (string length(3) <> 0) then (
			if (get sprite set number(sli)==6) then (
				set sprite set number (sli, 8)
			) else (set sprite set number (sli, 6))
		) else (
			if (get sprite set number(sli)==6) then (
				set sprite set number (sli, 7)
			) else (set sprite set number(sli, 6))
		)
	)
	
end

# calculate victory
script, slotwincheck, begin
	variable (i, sli, sli2, a, b, c)
	
	sli2 := initArray(3)
	
	a := get sprite frame(slice at pixel(firstchild (slicechild (firstchild (lookupslice (sli:slot)),0)),100,100))
	b := get sprite frame(slice at pixel(firstchild (slicechild (firstchild (lookupslice (sli:slot)),1)),200,100))
	c := get sprite frame(slice at pixel(firstchild (slicechild (firstchild (lookupslice (sli:slot)),2)),300,100))
	$3 = ""
	if (a == b && b == c ) then (
		# item ID is derived from sprite frame no.
		# make sure there are enough valid items
		# ID range: 0-2
		get item name (3,a)
		get item (a)
	)
	
	free slice (sli2)
end

# input
script, keypressSlots, begin
	variable (i, sli, dest)
	if (newkeypressA && state:slotspin) then (
		for (i,0,2) do (
			sli := firstchild (slicechild (firstchild (lookupslice (sli:slot)), i))
			if (get slice extra (sli,0)==1) then (
				set slice extra (sli, 0, 0)
				# snap to nearest multiple of 60
				dest := (slice y(sli)+60)/60*60+slotcenter
				move slice to (sli, sliceX(sli), dest, 30)
				break
			)
		)
	)
end