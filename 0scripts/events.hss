plotscript, events, which, begin
	suspendcontrol
	switch (currentmap) do (
		case (2) do (eventsattic(which))
	)
	resumecontrol
end

script, eventsattic, which, begin
	variable (i, var, who, sli, sli2, sli3, sli4)
	switch (which) do (
		case (0) do (
			if (check tag (tag:MirrorCheck)==on) then (
				write (2)
				sli := load attack sprite(0)
				center slice (sli)
				put slice (sli, 0, -20)
				for (i,0,2) do (
					setSpriteFrame (sli, i)
					specwait(8)
				)
				free slice (sli)
				specwait (80)
				sli := load attack sprite(1)
				sli2 := load attack sprite(2)
				set parent(sli, parent slice (getherosprite(me)))
				slice to back (sli)
				set parent(sli2,parent slice (getherosprite(me)))
				set slice visible (getherosprite(me), false)
				sli4 := create container (20, 36)
				set parent (sli4, sli)
				sli3 := loadWalkaboutSprite(get hero picture(me))
				set parent (sli3, sli4)
				realign slice (sli4, edge:center, edge:bottom, edge:center, edge:bottom)
				realign slice (sli3, edge:center, edge:bottom, edge:center, edge:bottom)
				realign slice (sli, edge:center, edge:bottom, edge:center, edge:bottom)
				realign slice (sli2, edge:center, edge:bottom, edge:center, edge:bottom)
				set slice clipping (sli4, true)
				dissolve sprite (sli, dissolve:shrink to center, 30, 10, true)
				dissolve sprite (sli2, dissolve:shrink to center, 30, 10, true)

				for (i, 1, 40) do (
					if (game:tick,mod,4==1) then (wildpal(4))
					set sprite frame (sli, (i/2),mod,2)
					set sprite frame (sli2, (i/2),mod,2)
					specwait(2)
				)
				for (i, 1,40) do (

					if (game:tick,mod,4==1) then (wildpal(4))
					specwait (2)
					setSlicey (sli3, i * -1)
					set sprite frame (sli, (i/2),mod,2)
					set sprite frame (sli2, (i/2),mod,2)
				)
				
				
				dissolve sprite (sli, dissolve:shrink to center, 30, 0)
				dissolve sprite (sli2, dissolve:shrink to center, 30, 0)
				for (i, 1, 10) do (
					if (game:tick,mod,4==1) then (wildpal(4))
					set sprite frame (sli, (i/2),mod,2)
					set sprite frame (sli2, (i/2),mod,2)
					specwait(2)
				)
				free slice (sli)
				free slice (sli2)
				reset palette
				update palette
				specwait (30)
				set slice visible (getherosprite(me), true)
			)else (
				who := NPC reference (0)
				set NPC frame(who, 1), specwait(5)
				set NPC frame(who, 0)
				for (i, 1, 3) do (
					set npc direction(who,i)
					specwait(10)
				)
				set tag (tag:MirrorCheck, on)
			)
			
		)
	)
end